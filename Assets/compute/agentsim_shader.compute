#pragma kernel CSMain
#pragma kernel ImageProcess

RWStructuredBuffer<float4> AgentData;
RWTexture2D<float4> Result;

float _dt;

int _width;
int _height;
float _time;
float _agentRadius;
float _threshold = 0.9;


float2 random2(float2 st){
    st = float2( dot(st,float2(127.1,311.7)),
              dot(st,float2(269.5,183.3)) );
    return -1.0 + 2.0*frac(sin(st)*43758.5453123);
}


float noise(float2 st) { // gradient noise
    float2 i = floor(st);
    float2 f = frac(st);

    float2 u = f*f*(3.0-2.0*f);

    return lerp( lerp( dot( random2(i + float2(0.0,0.0) ), f - float2(0.0,0.0) ),
                     dot( random2(i + float2(1.0,0.0) ), f - float2(1.0,0.0) ), u.x),
                lerp( dot( random2(i + float2(0.0,1.0) ), f - float2(0.0,1.0) ),
                     dot( random2(i + float2(1.0,1.0) ), f - float2(1.0,1.0) ), u.x), u.y);
}


// rotate vector v by angle a
float2 rotate(float2 v, float a)
{
    float s = sin(a);
    float c = cos(a);
    return float2(v.x * c - v.y * s, v.x * s + v.y * c);
}


float2 Sense(float2 pos, float2 vel)
{
    // Sample points 3 pixels ahead in different directions

    float angle = radians(30.0);
    float d = 5.0;

    float2 vl = normalize(rotate(vel, angle) * d);
    float2 vr = normalize(rotate(vel, -angle) * d);


    float valL = Result[int2(pos + vl * d)].r;
    float valC = Result[int2(pos + vel * d)].r;
    float valR = Result[int2(pos + vr * d)].r;

    // Determine which direction to turn based on the sensed values
    if (valL > valC && valL > valR)
    {
        // Turn left if left sample is strongest
        vel = lerp(vel,vl,valL);
    }
    else if (valR > valC && valR > valL)
    {
        // Turn right if right sample is strongest
        vel = lerp(vel,vr,valR);
    }
    // Keep going straight if center is strongest or values are equal

    return vel;
}

void bounceAgents(inout float2 pos,inout  float2 vel)
{

    if (pos.x <= 0 || pos.x >= _width)
    {
        vel.x = -vel.x;
        pos.x = clamp(pos.x, 0, _width);
    }

    if (pos.y <= 0 || pos.y >= _height)
    {
        vel.y = -vel.y;
        pos.y = clamp(pos.y, 0, _height);
    }
}

// agent update loop
[numthreads(64,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    float2 pos = AgentData[id.x].xy;
    float2 vel = AgentData[id.x].zw;


    // float2 seed1 = pos/200.0+ _time*0.1;
    // float2 seed2 = pos/200.0- _time*0.1;
    // float2 rand1 = float2(noise(seed1),noise(seed1+1732.34));
    // float2 rand2 = float2(noise(seed2),noise(seed2+1732.34));
    // float2 randPos = normalize(rand1+rand2);
    // float2 randi = normalize(float2(noise(id.x*1240.0),noise(id.y*12320.0+1732.34)));

    

    vel = Sense(pos, vel);

    // Move and bounce agent
   
    
    bounceAgents(pos, vel);

    pos += vel *_dt*0.1;

    // Update agent data
    AgentData[id.x] = float4(pos, vel);

    // Draw the agent at its position
    int2 pixelPos = int2(pos.x, pos.y);
    if (pixelPos.x >= 0 && pixelPos.x < _width && pixelPos.y >= 0 && pixelPos.y < _height)
    {
        Result[pixelPos] = float4(1, 0, 0, 1); // Set the pixel color to red for the agent
    }
}


[numthreads(8,8,1)]
void ImageProcess (uint3 id : SV_DispatchThreadID)
{

    float4 color = Result[id.xy];

    float fadeFactor = 0.005;
    
    // blur the color by averaging the color of the neighboring pixels
    float4 sum = float4(0, 0, 0, 0);
    int count = 0;
    int range = 1;
    for (int i = -range; i <= range; i++) // swap this for three cel evaluation using the vel to calculate the direction
    {
        for (int j = -range; j <= range; j++)
        {
            int2 neighborPos = int2(id.xy.x + i, id.xy.y + j);
            if (neighborPos.x >= 0 && neighborPos.x < _width && neighborPos.y >= 0 && neighborPos.y < _height)
            {
                sum += Result[neighborPos];
                count++;
            }
        }
    }
    color = sum / count;

    // Write the faded color back to the result
    Result[id.xy] = color-fadeFactor;
}
